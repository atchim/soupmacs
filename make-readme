#!/usr/bin/env fennel

(local fennel (require :fennel))

(fn make-md [f hn node]
  (when
    (and
      ; Performs some duck typing.
      (fennel.list? node)
      (not (fennel.sym? node))
      (= :fn (tostring (. node 1)))
      (fennel.sequence? (. node 3))
      (= :string (type (. node 4))))
    (let
      [ name (tostring (. node 2))
        params (fennel.view (. node 3))
        params (if (= "{}" params) "[]" params)
        docs (. node 4)]
      (when (= 1 (name:find :M. 1 true)) ; Filter module functions.
        (let
          [ hashes (: :# :rep hn)
            name (name:gsub :^M%. "")
            header (.. hashes " `" name " " params "`\n")
            (_ _ offset) (docs:find "\n\n(%s+)")
            docs (if offset (docs:gsub (.. "\n" offset) "\n") docs)
            docs (docs:gsub "\n(#+)" #(.. "\n" hashes $1))]
          (f (.. header "\n" docs)))))))

(fn walk [parser f]
  (fn walk-inner []
    (let [(ok? node) (parser)]
      (when ok?
        (f node)
        (walk-inner))))
  (walk-inner))

(let
  [ md
      "# Soup Macros

> A collection of useful macros for [Fennel].

## Usage

Pass the `--add-macro-path` argument with the path to the `soupmacs.fnl` file
to the `fennel` command in order to be able to import and use the macros. The
following snippets exemplify that.

```bash
fennel --add-macro-path $PATH_TO_SOUPMACS_FNL -c foo.fnl >foo.lua
```

```fennel
; Contents of file \"foo.fnl\".
(import-macros {: modcall} :soupmacs)
(modcall :bar :baz (:quux :corge))
```

## Macros

[Fennel]: https://fennel-lang.org"
    hn 3]

  (var macs-md "")

  (with-open [f (assert (io.open :soupmacs.fnl :r))]
    (let
      [ text (assert (f:read :*all))
        parser (fennel.parser (fennel.string-stream text))]
      (walk
        parser
        (partial make-md #(set macs-md (.. macs-md "\n\n" $1)) hn))))

  (let [(start end) (md:find "## Macros")]
    (assert start)
    (let [md (.. (md:sub 1 end) macs-md (md:sub (+ 1 end)))]
      (with-open [f (assert (io.open :README.md :w+))]
        (f:write md)))))
